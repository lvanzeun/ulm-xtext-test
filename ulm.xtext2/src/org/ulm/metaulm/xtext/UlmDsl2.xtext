grammar org.ulm.metaulm.xtext.UlmDsl2 with org.eclipse.xtext.common.Terminals

generate ulmDsl2 "http://org.mycorp/ulm/metaulm/xtext/UlmDsl2"

Model:
	'model' name=ID '{'
	contexts+=Context*
	'}';

Context:
	'context' name=ID version=Version '{'
	('attributes' '{'
	attributes+=Attribute*
	'}')?
	('lookups' '{'
	lookups+=Lookup*
	'}')?
	('entities' '{'
	entities+=Entity*
	'}')?
	'}';

Version:
	INT '.' INT '.' INT;

Attribute:
	name=ID type=(AttributeType | AttributeStringType | AttributeDecimalType ) (desc=STRING)? ';';

AttributeType:
	name=( 'int' | 'boolean' | 'date' | 'datetime' | 'timestamp' );

AttributeStringType:
	name='string' (array?='[' length=INT ']')?;
	
AttributeDecimalType:
	name='decimal' (array?='[' scale=INT (',' precision=INT)? ']')?;

Entity:
	type=('data' | 'static' | 'lookup')? name=ID (desc=STRING)? ('extends' superType=[Entity])? '{'
	features+=Feature*
	'}';

Feature:
	type=FeatureType
	(mandatory?='mandatory')?
	(identifier?='identifier')?
	('as' name=ID)?
	';';

FeatureType:
	type=(AttributeFeatureType | EntityFeatureType);

AttributeFeatureType:
	attribute=[Attribute] ('bounded' lookup=[Lookup])?;

EntityFeatureType:
	'references' entity=[Entity]  (array?='[' (length=INT)? ']')?;
	
Lookup:
	name=ID type=(LookupInt | LookupString)
;

LookupInt:
	'integer' (description=STRING)? 'values' '{' values+=LookupIntValue ( "," values+=LookupIntValue)* '}' ';' 
;

LookupString:
	'string' (description=STRING)? 'values' '{' values+=LookupStringValue ( "," values+=LookupStringValue)* '}' ';' 
;

LookupIntValue:
	{LookupIntValue}
	'(' value=INT ':' description=STRING ')';

LookupStringValue:
	{LookupStringValue}
	'(' value=STRING ':' description=STRING	')';
